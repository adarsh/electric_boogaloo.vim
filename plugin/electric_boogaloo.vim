" Insert a newline after each specified string (or before if use '!').
" If no arguments, use previous search.
" Original source: http://vim.wikia.com/wiki/Add_a_newline_after_given_patterns

command! -bang -nargs=* -range Breakin <line1>,<line2>call Breakin('<bang>', <f-args>)
function! Breakin(bang, ...) range
  let save_search = @/
  if empty(a:bang)
    let before = ''
    let after = '\ze.'
    let repl = '&\r'
  else
    let before = '.\zs'
    let after = ''
    let repl = '\r&'
  endif
  let pat_list = map(deepcopy(a:000), "escape(v:val, '/\\.*$^~[')")
  let find = empty(pat_list) ? @/ : join(pat_list, '\|')
  let find = before . '\%(' . find . '\)' . after
  " Example: 10,20s/\%(arg1\|arg2\|arg3\)\ze./&\r/ge
  execute a:firstline . ',' . a:lastline . 's/'. find . '/' . repl . '/ge'
  let @/ = save_search
endfunction
